plugins {
    id 'fabric-loom' version '1.5-SNAPSHOT'
    id 'maven-publish'
}

version = '0.0.1'
group = 'micheal65536.fountain.fabric'

allprojects {
    apply plugin: 'fabric-loom'

    version = rootProject.version
    group = rootProject.group

    loom {
        splitEnvironmentSourceSets()

        mods.create(project.name, {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        })
    }

    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "id": project.name, "version": project.version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    repositories {
        mavenLocal()
    }

    dependencies {
        minecraft "com.mojang:minecraft:1.20.4"
        mappings "net.fabricmc:yarn:1.20.4+build.1:v2"
        modImplementation "net.fabricmc:fabric-loader:0.15.0"
        modImplementation "net.fabricmc.fabric-api:fabric-api:0.91.1+1.20.4"
    }

    publishing {
        publications {
            create(project.name, MavenPublication, {
                artifactId project.name

                from components.java
            })
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "id": "${project.name}", "version": project.version
    }
}

dependencies {
    afterEvaluate {
        subprojects.each {
            api project(path: it.path, configuration: 'namedElements')
        }
    }
}

remapJar {
    afterEvaluate {
        subprojects.each {
            nestedJars.from project(it.path).tasks.getByName('remapJar')
        }
    }
}

publishing {
    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}